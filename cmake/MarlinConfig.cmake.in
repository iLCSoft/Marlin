##############################################################################
# cmake configuration file for Marlin
#
# requires:
#   MacroCheckPackageLibs.cmake for checking package libraries
#   MacroExportPackageDeps.cmake for exporting package dependencies
#
# returns following variables:
#
#   Marlin_FOUND      : set to TRUE if Marlin found
#       if FIND_PACKAGE called with REQUIRED and COMPONENTS arguments
#       Marlin_FOUND is only set to TRUE if ALL components are also found
#       if REQUIRED is NOT set components may or may not be available
#
#   Marlin_ROOT       : path to this Marlin installation
#   Marlin_VERSION    : package version
#   Marlin_LIBRARIES  : list of Marlin libraries (NOT including COMPONENTS)
#   Marlin_INCLUDE_DIRS  : list of paths to be used with INCLUDE_DIRECTORIES
#   Marlin_LIBRARY_DIRS  : list of paths to be used with LINK_DIRECTORIES
#   Marlin_COMPONENT_LIBRARIES      : list of Marlin component libraries
#   Marlin_${COMPONENT}_FOUND       : set to TRUE or FALSE for each library
#   Marlin_${COMPONENT}_LIBRARY     : path to individual libraries
#   Marlin_${COMPONENT}_LIB_DEPENDS : individual library dependencies
#
# @author Jan Engels, Desy
##############################################################################

SET( Marlin_ROOT "@CMAKE_INSTALL_PREFIX@" )
SET( Marlin_VERSION "@Marlin_VERSION@" )


# ---------- include dirs -----------------------------------------------------
# do not store find results in cache
SET( Marlin_INCLUDE_DIRS Marlin_INCLUDE_DIRS-NOTFOUND )
MARK_AS_ADVANCED( Marlin_INCLUDE_DIRS )

FIND_PATH( Marlin_INCLUDE_DIRS
	NAMES marlin/Processor.h
	PATHS ${Marlin_ROOT}/include
	NO_DEFAULT_PATH
)



# ---------- libraries --------------------------------------------------------
INCLUDE( "@ILCSOFT_CMAKE_MODULES_ROOT@/MacroCheckPackageLibs.cmake" )

# only standard libraries should be passed as arguments to CHECK_PACKAGE_LIBS
# additional components are set by cmake in variable PKG_FIND_COMPONENTS
# first argument should be the package name
CHECK_PACKAGE_LIBS( Marlin Marlin MarlinXML )



# ---------- dependencies -----------------------------------------------------
INCLUDE( "@ILCSOFT_CMAKE_MODULES_ROOT@/MacroExportPackageDeps.cmake" )
# exports following package dependencies (if set)
# first argument of macro should be the package name
SET( Marlin_DEPENDS_INCLUDE_DIRS  @Marlin_DEPENDS_INCLUDE_DIRS@ )
SET( Marlin_DEPENDS_LIBRARY_DIRS  @Marlin_DEPENDS_LIBRARY_DIRS@ )
SET( Marlin_DEPENDS_LIBRARIES  @Marlin_DEPENDS_LIBRARIES@ )
EXPORT_PACKAGE_DEPENDENCIES( Marlin )



# ---------- final checking ---------------------------------------------------
INCLUDE( FindPackageHandleStandardArgs )
# set MARLIN_FOUND to TRUE if all listed variables are TRUE and not empty
# Marlin_COMPONENT_VARIABLES will be set if FIND_PACKAGE is called with REQUIRED argument
FIND_PACKAGE_HANDLE_STANDARD_ARGS( Marlin DEFAULT_MSG Marlin_ROOT Marlin_INCLUDE_DIRS Marlin_LIBRARIES ${Marlin_COMPONENT_VARIABLES} )

SET( Marlin_FOUND ${MARLIN_FOUND} )



# ---------- definitions ------------------------------------------------------
IF( MARLIN_FOUND )

    IF( CMAKE_BUILD_TYPE STREQUAL "Debug" OR
        CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo" )
        SET( Marlin_DEFINITIONS "-DMARLINDEBUG" )
        MARK_AS_ADVANCED( Marlin_DEFINITIONS )
    ENDIF()

ENDIF()

